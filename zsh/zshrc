#
# User configuration sourced by interactive shells
#

# ensure (F)PATH entries remain unique
typeset -U path fpath

# Editor
# In absence of explicit zle configuration (e.g. via bindkey), Zsh sets its key bindings based on the value of $EDITOR.
# We set it first to be on the safe side.
export EDITOR=vi
export VISUAL=vi

# Fpath mangling for user functions
fpath=(${HOME}/.local/share/zsh/site-functions $fpath)


# -----------------
# Zsh configuration
# -----------------

#
# History
#

# Remove older command from the history if a duplicate is to be added.
setopt HIST_IGNORE_ALL_DUPS

#
# Input/output
#

# Set editor default keymap to emacs (`-e`) or vi (`-v`)
bindkey -v

# Prompt for spelling correction of commands.
#setopt CORRECT

# Customize spelling correction prompt.
#SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

# Remove path separator from WORDCHARS.
WORDCHARS=${WORDCHARS//[\/]}

# -----------------
# Zim configuration
# -----------------

# Use degit instead of git as the default tool to install and update modules.
#zstyle ':zim:zmodule' use 'degit'

# --------------------
# Module configuration
# --------------------

#
# git
#

# Set a custom prefix for the generated aliases. The default prefix is 'G'.
#zstyle ':zim:git' aliases-prefix 'g'

#
# input
#

# Append `../` to your input for each `.` you type after an initial `..`
#zstyle ':zim:input' double-dot-expand yes

#
# termtitle
#

# Set a custom terminal title format using prompt expansion escape sequences.
# See http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html#Simple-Prompt-Escapes
# If none is provided, the default '%n@%m: %~' is used.
#zstyle ':zim:termtitle' format '%1~'
zstyle ':zim:termtitle' hooks 'preexec' 'precmd'
zstyle ':zim:termtitle:preexec' format '${(V)2}'

#
# zsh-autosuggestions
#

# Disable automatic widget re-binding on each precmd. This can be set when
# zsh-users/zsh-autosuggestions is the last module in your ~/.zimrc.
ZSH_AUTOSUGGEST_MANUAL_REBIND=1

# Customize the style that the suggestions are shown with.
# See https://github.com/zsh-users/zsh-autosuggestions/blob/master/README.md#suggestion-highlight-style
#ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=242'

#
# zsh-syntax-highlighting
#

# Set what highlighters will be used.
# See https://github.com/zsh-users/zsh-syntax-highlighting/blob/master/docs/highlighters.md
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)

# Customize the main highlighter styles.
# See https://github.com/zsh-users/zsh-syntax-highlighting/blob/master/docs/highlighters/main.md#how-to-tweak-it
#typeset -A ZSH_HIGHLIGHT_STYLES
#ZSH_HIGHLIGHT_STYLES[comment]='fg=242'

# ------------------
# Initialize modules
# ------------------

ZIM_HOME=${ZDOTDIR:-${HOME}}/.zim
# Download zimfw plugin manager if missing.
if [[ ! -e ${ZIM_HOME}/zimfw.zsh ]]; then
	if (( ${+commands[curl]} )); then
		curl -fsSL --create-dirs -o ${ZIM_HOME}/zimfw.zsh \
			https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
	else
		mkdir -p ${ZIM_HOME} && wget -nv -O ${ZIM_HOME}/zimfw.zsh \
			https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
	fi
fi
# Install missing modules, and update ${ZIM_HOME}/init.zsh if missing or outdated.
if [[ ! ${ZIM_HOME}/init.zsh -nt ${ZIM_CONFIG_FILE:-${ZDOTDIR:-${HOME}}/.zimrc} ]]; then
	source ${ZIM_HOME}/zimfw.zsh init -q
fi
# Initialize modules.
source ${ZIM_HOME}/init.zsh

# ------------------------------
# Post-init module configuration
# ------------------------------

#
# zsh-history-substring-search
#

zmodload -F zsh/terminfo +p:terminfo
# Bind ^[[A/^[[B manually so up/down works both before and after zle-line-init
for key ('^[[A' '^P' ${terminfo[kcuu1]}) bindkey ${key} history-substring-search-up
for key ('^[[B' '^N' ${terminfo[kcud1]}) bindkey ${key} history-substring-search-down
for key ('k') bindkey -M vicmd ${key} history-substring-search-up
for key ('j') bindkey -M vicmd ${key} history-substring-search-down
unset key

# ------------------------------
# Custom post-init configuration
# ------------------------------

# GPG
GPG_TTY="$(tty)"
export GPG_TTY

# Path mangling for user executables (Python, etc.)
path=(${HOME}/.local/bin $path)

# Go
export GOPATH=${HOME}/go
path=(${GOPATH}/bin $path)

# WezTerm
path=(${WEZTERM_EXECUTABLE_DIR} $path)

# Allow command line editing in an external editor in the "vicmd" keymap.
# Zim's "input" module already enables this in the default keymap ("viins").
bindkey -M vicmd "${key_info[Control]}x${key_info[Control]}e" edit-command-line

# Commands completion
(( $+commands[limactl] )) && . <(limactl completion zsh) && compdef _limactl limactl

# Aliases
alias Gid='git diff --cached'
alias GiD='git diff --cached --word-diff'
alias Gwd='git diff'
alias GwD='git diff --word-diff'
alias Grif=__git_rebase_fuzzy
__git_rebase_fuzzy() {
	local selected commit
	local -i count=25
	if (($# == 1)); then
		count=$1
	fi
	selected="$(git log -${count} --date-order --pretty=format:'%h %s%d' \
		| fzf -0 --no-multi --preview='git show --color=always {1}')"
	local ret=$?
	if [ -z ${selected} ]; then
		return $ret
	fi
	commit=$(awk '{print $1}' <<< ${selected})
	git rebase --interactive --autosquash "${commit}^"
}

# Shell theming
fzf_default_opts=(${FZF_DEFAULT_OPTS} '--color=16' '--color=fg+:7,hl:3,hl+:11,prompt:6,pointer:4,marker:5,info:2')
export FZF_DEFAULT_OPTS=${fzf_default_opts}
unset fzf_default_opts

# Difftastic
export DFT_BACKGROUND=light  # disable usage of bright ANSI colors

# Terminal shell integration
if [[ ${TERM_PROGRAM} == WezTerm ]]; then
	if [[ -s ${WEZTERM_EXECUTABLE_DIR:h}/Resources/wezterm.sh ]]; then
		. ${WEZTERM_EXECUTABLE_DIR:h}/Resources/wezterm.sh
	elif [[ -s ${HOME}/.wezterm.sh ]]; then
		. ${HOME}/.wezterm.sh
	fi
fi

# Sets a fixed termtitle for the current terminal
termtitle() {
	local zhooks zhook
	zstyle -a ':zim:termtitle' hooks 'zhooks' || zhooks=(precmd)
	for zhook in ${zhooks}; do
		add-zsh-hook -d ${zhook} termtitle_update_${zhook}
	done
	print -rn $'\e]2;'${(q)@}$'\a'
}

# Source local environment
if [[ -s ${ZDOTDIR:-${HOME}}/.zshrc.local ]]; then
	. ${ZDOTDIR:-${HOME}}/.zshrc.local
fi
